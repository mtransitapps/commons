// https://developer.android.com/studio/build#settings-file
// https://docs.gradle.org/current/userguide/declaring_repositories.html#sec:repository-content-filtering
// https://docs.gradle.org/current/userguide/declaring_repositories.html#sec:declaring-repository-filter
pluginManagement {
    repositories {
        google {
            content {
                includeGroupByRegex("androidx\\..*")
                includeGroupByRegex("com\\.android.*")
                includeGroupByRegex("com\\.google\\.android\\.gms.*")
                includeGroupByRegex("com\\.google\\.firebase.*")
                includeGroupByRegex("com\\.google\\.gms.*")
                includeGroupByRegex("com\\.google\\.test.*")
            }
        }
        gradlePluginPortal {
            content {
                includeGroupByRegex("com\\.github\\.ben\\-manes.*")
                includeGroupByRegex("com\\.github\\.triplet.*")
                includeGroupByRegex("com\\.google\\.protobuf.*")
            }
        }
        mavenCentral()
    }
}

// https://developer.android.com/build/dependencies#remote-repositories
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google {
            content {
                includeGroupByRegex("androidx\\..*")
                includeGroupByRegex("com\\.android.*")
                includeGroupByRegex("com\\.google\\.ads.*")
                includeGroupByRegex("com\\.google\\.android.*")
                includeGroupByRegex("com\\.google\\.android\\.gms.*")
                includeGroupByRegex("com\\.google\\.firebase.*")
            }
        }
        mavenCentral()
    }
    versionCatalogs {
        libs {
            from(files("commons/gradle/libs.versions.toml"))
        }
    }
}

rootProject.name = "MT" // same JetBrains IntelliJ files

include(":commons") // common code shared by all
include(":commons-java") // common code shared by all projects

if (file("agency-parser").exists()) {
    include(":agency-parser") // parser
}

if (file("parser").exists()) {
    include(":parser") // common code shared by all parsers
}

include(":commons-android") // common code shared by all Android apps
include(":app-android") // Android app
apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

Properties projectNameProperties = new Properties()
projectNameProperties.load(new FileInputStream(file("project_name.properties")))

def projectName = projectNameProperties["project.name"]
println "> App project name: " + projectName

if (projectName == "MT") {
    apply plugin: 'io.fabric'
    apply plugin: "org.sonarqube"
}

dependencies {
    if (projectName == "MT") {
        // ALL
        implementation(project(':commons-android')) {
            exclude group: 'com.google.protobuf', module: 'protobuf-java'
        }
        implementation rootProject.ext.supportLibs.appCompat
        implementation rootProject.ext.supportLibs.activity
        implementation rootProject.ext.supportLibs.fragment
        implementation rootProject.ext.supportLibs.customTabs
        implementation rootProject.ext.supportLibs.design
        implementation rootProject.ext.supportLibs.swiperefreshlayout
        implementation rootProject.ext.supportLibs.vectordrawable
        implementation rootProject.ext.gpsLibs.basement // from commons-android
        implementation rootProject.ext.gpsLibs.location
        implementation rootProject.ext.gpsLibs.maps
        implementation rootProject.ext.gpsLibs.firebase_ads
        implementation rootProject.ext.gpsLibs.firebase_analytics
        implementation rootProject.ext.gpsLibs.firebase_core
        implementation(rootProject.ext.gpsLibs.firebase_crashlytics_aar) {
            transitive = true
        }
        // implementation rootProject.ext.gpsLibs.mediation_test_suite
        implementation rootProject.ext.fbLibs.values()
        implementation(rootProject.ext.moPubLibs.moPubAdsBanner) {
            transitive = true
        }
        implementation rootProject.ext.moPubLibs.moPubAdsMediation
        // DEBUG
        debugImplementation rootProject.ext.leakCanaryLibs.leakcanary
        // DEBUG
        debugImplementation rootProject.ext.supportLibs.multidex

        testImplementation rootProject.ext.jUnitLibs.jUnit
        testImplementation rootProject.ext.mockitoLibs.mockitoCore
    } else {
        implementation project(':commons-android')
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility rootProject.javaVersion
        targetCompatibility rootProject.javaVersion
    }

    Properties versionProperties = new Properties()
    versionProperties.load(new FileInputStream(file("../commons-android/version.properties")))
    def theVersionName = versionProperties['version.name']
    println "> App version name: " + theVersionName

    def commitCountS = "git -C ../ rev-list HEAD --count".execute().text.trim()
    def theVersionCode = 1
    if (!commitCountS.empty) {
        def commitCount = commitCountS.toInteger()
        theVersionCode = 1000 + commitCount
    }
    println "> App version code: " + theVersionCode

    def useGooglePlayUploadKeysProperties = true
    if (project.hasProperty("useGooglePlayUploadKeysProperties")) {
        useGooglePlayUploadKeysProperties = project.getProperty("useGooglePlayUploadKeysProperties").toBoolean()
    }
    println "> Using Google Play Upload keys: " + useGooglePlayUploadKeysProperties

    Properties appSigningKeysProperties = new Properties()
    def appSigningKeysPropertiesFile = file("app-signing-release-keys.properties")
    if (appSigningKeysPropertiesFile.exists()) {
        appSigningKeysProperties.load(new FileInputStream(appSigningKeysPropertiesFile))
    } else {
        appSigningKeysPropertiesFile = file("../app-signing-release-keys.properties")
        if (appSigningKeysPropertiesFile.exists()) {
            appSigningKeysProperties.load(new FileInputStream(appSigningKeysPropertiesFile))
        }
    }

    Properties googlePlayUploadKeysProperties = new Properties()
    def googlePlayUploadKeysPropertiesFile = file("google-play-upload-keys.properties")
    if (googlePlayUploadKeysPropertiesFile.exists()) {
        googlePlayUploadKeysProperties.load(new FileInputStream(googlePlayUploadKeysPropertiesFile))
    } else {
        googlePlayUploadKeysPropertiesFile = file("../google-play-upload-keys.properties")
        if (googlePlayUploadKeysPropertiesFile.exists()) {
            googlePlayUploadKeysProperties.load(new FileInputStream(googlePlayUploadKeysPropertiesFile))
        }
    }

    def resValueFr = file("src/main/res/values-fr")
    println "> Including French language: " + resValueFr.exists()

    defaultConfig {
        versionCode theVersionCode
        versionName theVersionName + "r" + theVersionCode
        if (useGooglePlayUploadKeysProperties) {
            setProperty("archivesBaseName", "${projectName}_v${theVersionName}_r${theVersionCode}_upload")
        } else {
            setProperty("archivesBaseName", "${projectName}_v${theVersionName}_r${theVersionCode}")
        }
        if (resValueFr.exists()) {
            resConfigs "en", "fr"
        } else {
            resConfigs "en"
        }
        // resConfigs "en", "xxxhdpi" // DEBUG xxxhdpi = Pixel 2 XL
        // resConfigs "fr", "xxhdpi" // DEBUG xxhdpi = Nexus 5
        minSdkVersion rootProject.ext.minSdkVersion
        // minSdkVersion 21 // DEBUG avoid using legacy multidex
        targetSdkVersion rootProject.ext.targetSdkVersion
        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = [
                permission_provider_read     : "org.mtransit.android.provider.permission.READ_PROVIDER",
                permission_receiver_broadcast: "org.mtransit.android.receiver.permission.BROADCAST_RECEIVER"
        ]
    }

    signingConfigs {
        release {
            if (useGooglePlayUploadKeysProperties) {
                if (googlePlayUploadKeysPropertiesFile.exists()) {
                    storeFile file(googlePlayUploadKeysProperties['key.store'])
                    storePassword googlePlayUploadKeysProperties['key.store.password']
                    keyAlias googlePlayUploadKeysProperties['key.alias']
                    keyPassword googlePlayUploadKeysProperties['key.alias.password']
                }
            } else {
                if (appSigningKeysPropertiesFile.exists()) {
                    storeFile file(appSigningKeysProperties['key.store'])
                    storePassword appSigningKeysProperties['key.store.password']
                    keyAlias appSigningKeysProperties['key.alias']
                    keyPassword appSigningKeysProperties['key.alias.password']
                }
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "_DEBUG"
            applicationIdSuffix ".debug"
            manifestPlaceholders = [
                    permission_provider_read     : "org.mtransit.android.debug.provider.permission.READ_PROVIDER",
                    permission_receiver_broadcast: "org.mtransit.android.debug.receiver.permission.BROADCAST_RECEIVER"
            ]

            // testCoverageEnabled false // DEBUG skip coverage report
            shrinkResources false
            if (projectName == "MT") {
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android.txt'),
                        'proguard-rules.pro'
                multiDexEnabled true
                // Disable fabric build ID generation for debug builds
                ext.enableCrashlytics = false
                // https://docs.fabric.io/android/crashlytics/build-tools.html
                ext.alwaysUpdatedBuildId = false
            } else {
                minifyEnabled false
            }
            aaptOptions.cruncherEnabled = false
        }
        //noinspection GroovyMissingReturnStatement
        release {
            if (useGooglePlayUploadKeysProperties) {
                if (googlePlayUploadKeysPropertiesFile.exists()) {
                    signingConfig signingConfigs.release
                }
            } else {
                if (appSigningKeysPropertiesFile.exists()) {
                    signingConfig signingConfigs.release
                }
            }

            if (projectName == "MT") {
                shrinkResources true
                minifyEnabled true
                proguardFiles getDefaultProguardFile(
                        'proguard-android-optimize.txt'), // 'proguard-android.txt'),
                        'proguard-rules.pro'
            } else {
                shrinkResources false
                minifyEnabled false
            }
        }
    }

    //noinspection GroovyMissingReturnStatement
    lintOptions {
        if (projectName == "MT") {
            abortOnError true
            warningsAsErrors false
            ignoreWarnings false
            quiet false
        } else {
            abortOnError false
        }
        checkDependencies true // also check leaf modules
        // checkAllWarnings true // too slow for CI, can be enabled locally
        ignoreTestSources true
        if (projectName == "MT") {
            warning 'InvalidPackage', //
                    'ClickableViewAccessibility', //
                    'MergeRootFrame', //
                    'Overdraw', //
                    'UselessParent', //
                    'IconDensities',  //
                    'RtlHardcoded'
        }
    }

    sourceSets {
        main {
            if (projectName == "MT") {
                // NOTHING
            } else {
                res.srcDirs += 'src/main/res-current'
                res.srcDirs += 'src/main/res-next'
            }
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                jvmArgs '-noverify'
                testLogging {
                    events 'passed', 'skipped', 'failed'
                }
            }
        }
    }
}

play {
    serviceAccountCredentials = file("google-play-auto-publisher.json")
    defaultToAppBundles = true
    // Available tracks: internal, alpha, Beta (Private), beta, production
    // "Beta (Private)" is default because it's hard to use with -Pparam parameter
    track = "Beta (Private)"
    fromTrack = "Beta (Private)"
    promoteTrack = "Beta (Private)"
    releaseStatus = "draft"// completed/draft/inProgress/halted
    userFraction = 0.01 // 0.00 - 0.10 - 0.33 - 0.50 - 1.00 WHEN releaseStatus=[inProgress/halted]
}

if (projectName == "MT") {
    apply plugin: 'com.google.gms.google-services'
}
